
#load-if-not-defined DEFEND-WONDER
#load-if-not-defined WONDER-RACE
#load-if-defined DIFFICULTY-MODERATE
(load-random
        20      "ai-original\ai-original_dip-bully"
        5       "ai-original\ai-original_dip-boomer"
        5       "ai-original\ai-original_dip-feeder"
)

#else

(load-random
        10      "ai-original\ai-original_dip-bully"
        10      "ai-original\ai-original_dip-boomer"
        10      "ai-original\ai-original_dip-feeder"
)
#end-if
#end-if
#end-if

#load-if-defined WONDER-RACE
(load-random
        25      "ai-original\ai-original_dip-feeder"
)
#end-if

#load-if-defined DEFEND-WONDER

(load-random
        15      "ai-original\ai-original_dip-feeder"
)

#end-if


;**************************************
;GO NAVY
(defrule
        (taunt-detected any-ally 34)
        (goal 1 0)
=>
        (acknowledge-taunt this-any-ally 34)
        (chat-to-player-using-id this-any-ally 22158)
                ;"39 My sensors aren't detecting any bodies of water, commander."
)

(defrule
        (taunt-detected any-ally 34)
        (or
                (goal 1 1)
                (goal 1 10)
        )
=>
        (set-goal sea-owner YES)
        (acknowledge-taunt this-any-ally 34)
        (chat-to-player-using-id this-any-ally 22153)
                ;"39 At once, commander!"
)

;**************************************
;GO LAND
(defrule
        (taunt-detected any-ally 35)
        (goal 1 0)
=>
        (acknowledge-taunt this-any-ally 35)
        (chat-to-player-using-id this-any-ally 22159)
                ;"39 Yes, I had that under control.")
)

(defrule
        (taunt-detected any-ally 35)
        (or
                (goal 1 1)
                (goal 1 10)
        )
=>
        (set-goal sea-owner NOTICE)
        (acknowledge-taunt this-any-ally 35)
        (chat-to-player-using-id this-any-ally 22153)
                ;"39 At once, commander!"
)


;**************************************
;WHAT AGE ARE YOU? (taunt 42)

(defrule
        (taunt-detected any-ally 42)
        (current-age == tech-level-1)
=>
        (acknowledge-taunt this-any-ally 42)
        (chat-to-player-using-id this-any-ally 22165)
                ;"39 We're still firmly in tech level 1."
)

(defrule
        (taunt-detected any-ally 42)
        (current-age == tech-level-2)
=>
        (acknowledge-taunt this-any-ally 42)
        (chat-to-player-using-id this-any-ally 22166)
                ; "39 We just achieved tech level 2."
)

(defrule
        (taunt-detected any-ally 42)
        (current-age == tech-level-3)
=>
        (acknowledge-taunt this-any-ally 42)
        (chat-to-player-using-id this-any-ally 22167)
                ; "39 Commander, we've achieved tech level 3."
)

(defrule
        (taunt-detected any-ally 42)
        (current-age == tech-level-4)
=>
        (acknowledge-taunt this-any-ally 42)
        (chat-to-player-using-id this-any-ally 22168)
                ; "39 Finally, commander, we've reached tech level 4."
)


;**************************************
;BASIC DIPLOMACY
#load-if-not-defined TEAMS-LOCKED
(defrule
        (true)
=>
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (disable-self)
)

(defrule
        (current-age >= tech-level-2)
        (players-stance any-computer-ally neutral)
=>
        (set-stance this-any-computer-ally neutral)
)

(defrule
        (current-age >= tech-level-2)
        (players-stance any-computer-ally enemy)
=>
        (set-stance this-any-computer-ally enemy)
)

(defrule
        (current-age >= tech-level-2)
        (players-stance any-computer-neutral ally)
=>
        (set-stance this-any-computer-neutral ally)
)

(defrule
        (current-age >= tech-level-2)
        (players-stance any-computer-neutral enemy)
=>
        (set-stance this-any-computer-neutral enemy)
)

(defrule
        (current-age >= tech-level-2)
        (players-stance any-computer-enemy ally)
=>
        (set-stance this-any-computer-enemy ally)
)

(defrule
        (current-age >= tech-level-2)
        (players-stance any-computer-enemy neutral)
=>
        (set-stance this-any-computer-enemy neutral)
)

;*******************************************************
;is there more than one human player?
(defrule
        (player-human 1)
        (or
                (or
                        (player-human 2)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 7)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

(defrule
        (player-human 2)
        (or
                (or
                        (player-human 1)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 7)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

(defrule
        (player-human 3)
        (or
                (or
                        (player-human 2)
                        (player-human 1)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 7)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

(defrule
        (player-human 4)
        (or
                (or
                        (player-human 2)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 1)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 7)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

(defrule
        (player-human 5)
        (or
                (or
                        (player-human 2)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 1)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 7)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

(defrule
        (player-human 6)
        (or
                (or
                        (player-human 2)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 1)
                                        (player-human 7)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

(defrule
        (player-human 7)
        (or
                (or
                        (player-human 2)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 1)
                                )
                        )
                        (player-human 8)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)
(defrule
        (player-human 8)
        (or
                (or
                        (player-human 2)
                        (player-human 3)
                )
                (or
                        (or
                                (or
                                        (player-human 4)
                                        (player-human 5)
                                )
                                (or
                                        (player-human 6)
                                        (player-human 1)
                                )
                        )
                        (player-human 1)
                )
        )
=>
        (set-goal get-mad YES)
        (disable-self)
)

;*******************************************************
;BASIC DIPLOMACY VS. HUMANS
;if the human player doesn't go neutral at our request
(defrule
        (goal get-mad NO)
        (timer-triggered t-chathelp)
        (players-stance any-human-neutral enemy)
        (or
                (difficulty <= moderate)
                (current-age > tech-level-1)
        )
=>
        (chat-to-player-using-id this-any-human-neutral 22027)
                ;"41 We are Neutral to you.  Please set your diplomacy to Neutral to us."
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (set-goal get-mad ANGRY)
)

(defrule
        (or
                (goal get-mad ANGRY) 
                (or
                        (goal get-mad ANGRIER)
                        (goal get-mad ANGRIEST)
                )
        )
        (or
                (players-stance any-human-neutral neutral)
                (players-stance any-human-neutral ally)
        )
=>
        (chat-to-player-using-id this-any-human-neutral 22028)
                ;"39 Welcome aboard."
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (set-goal get-mad NO)
)

(defrule
        (goal get-mad ANGRY)
        (timer-triggered t-chathelp)
        (players-stance any-human-neutral enemy)
=>
        (set-goal get-mad ANGRIER)
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (chat-to-player-using-id this-any-human-neutral 22030)
                ;"41 So you choose to ignore my good will?  I'll give you one more chance."
)

(defrule
        (goal get-mad ANGRIER)
        (timer-triggered t-chathelp)
        (players-stance any-human-neutral enemy)
=>
        (set-goal get-mad ANGRIEST)
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (chat-to-player-using-id this-any-human-neutral 22032)
                ;"41 You do not have any other alternative - set your stance to Neutral towards me."
)

(defrule
        (goal get-mad ANGRIEST)
        (timer-triggered t-chathelp)
        (players-stance any-human-neutral enemy)
=>
        (set-goal get-mad YES)
        (chat-to-player-using-id this-any-human-neutral 22025)
                ;"40 We do not look kindly on your lack of good faith." 
        (set-stance this-any-human-neutral enemy)
        (set-stance every-computer ally)
        (disable-timer t-chathelp)
        (disable-self)
)

;when an ally turns on us
(defrule
        (goal get-mad NO)
        (or
                (players-stance any-human-ally neutral)
                (players-stance any-human-ally enemy)
        )
=>
        (chat-to-player-using-id this-any-human-ally 22147)
                ;"39 I have professed my friendship.  Now set your stance to Ally as well."
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (set-goal get-mad ANGRY)
)

(defrule
        (or
                (goal get-mad ANGRY) 
                (or
                        (goal get-mad ANGRIER)
                        (goal get-mad ANGRIEST)
                )
        )
        (players-stance any-human-ally ally)
=>
        (chat-to-player-using-id this-any-human-ally 22148)
                ;"39 Thanks for remaining my Ally."
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (set-goal get-mad NO)
)

(defrule
        (goal get-mad ANGRY)
        (timer-triggered t-chathelp)
        (or
                (players-stance any-human-ally neutral)
                (players-stance any-human-ally enemy)
        )
=>
        (set-goal get-mad ANGRIER)
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (chat-to-player-using-id this-any-human-ally 22149)
                ;"41 I sense a deception.  You're still not my Ally?"
)

(defrule
        (goal get-mad ANGRIER)
        (timer-triggered t-chathelp)
        (or
                (players-stance any-human-ally neutral)
                (players-stance any-human-ally enemy)
        )
=>
        (set-goal get-mad ANGRIEST)
        (disable-timer t-chathelp)
        (enable-timer t-chathelp 120)
        (chat-to-player-using-id this-any-human-ally 22151)
                ;"41 You're dark indeed.  I'm running out of patience."
)

(defrule
        (goal get-mad ANGRIEST)
        (timer-triggered t-chathelp)
        (or
                (players-stance any-human-ally neutral)
                (players-stance any-human-ally enemy)
        )
=>
        (set-goal get-mad YES)
        (chat-to-player-using-id this-any-human-ally 22025)
                ;"40 We do not look kindly on your lack of good faith." 
        (set-stance this-any-human-ally enemy)
        (set-stance every-computer ally)
        (disable-timer t-chathelp)
        (disable-self)
)

;end result -- go hostile!!
(defrule
        (stance-toward any-human neutral)
        (nor
                (player-in-game any-computer-ally)
                (or
                        (player-in-game any-computer-enemy)
                        (player-in-game any-computer-neutral)
                )
        )
=>
        (set-goal get-mad YES)
        (chat-to-player-using-id this-any-human 22034)
                ;"40 Your chances have just run out."
        (set-stance this-any-human enemy)
        (set-stance every-computer ally)
        (disable-timer t-chathelp)
        (disable-self)
)

(defrule
        (goal get-mad YES)
=>
        (set-goal personality 10)
        (set-stance every-human enemy)
        (set-stance every-computer ally)
        (disable-self)
)

(defrule
        (difficulty <= hard)
        (stance-toward any-human ally)
        =>
        (set-stance this-any-human enemy)
)

#end-if