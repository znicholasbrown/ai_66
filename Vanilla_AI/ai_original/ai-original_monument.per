
;**************************************
;BUILD A MONUMENT
(defrule
        (taunt-detected any-ally 37)
        (current-age < tech-level-4)
=>
        (acknowledge-taunt this-any-ally 37)
        (chat-to-player-using-id this-any-ally 22162)
                ;"39 I haven't advanced to tech level 4 yet, commander.")
)

(defrule
        (taunt-detected any-ally 37)
        (current-age == tech-level-4)
=>
        (set-goal upgrade-conflict NOTICE)
        (set-goal wonder-attempt POSSIBLE)
        (disable-timer t-ageup)
        (acknowledge-taunt this-any-ally 37)
        (chat-to-player-using-id every-computer 22410)  ;200
        (chat-to-player-using-id this-any-ally 22161)
                ;"39 We'll break ground for the Monument immediately, commander.")
)

;; monument ;;

;********************************
#load-if-not-defined DIFFICULTY-EASIEST
#load-if-not-defined DIFFICULTY-EASY
#load-if-not-defined VICTORY-CONQUEST

(defrule
        (goal personality 1)
        (not (goal wonder-attempt NO) )
=>
        (set-goal wonder-attempt NO)
)

(defrule
        (goal wonder-attempt MAYBE)
        (taunt-detected any-ally 200)
=>
        (set-goal wonder-attempt NO)
        (set-goal upgrade-conflict NO)
        (acknowledge-taunt this-any-ally 200)
        (disable-self)
)

;should we construct a monument?
(defrule
        (goal wonder-attempt MAYBE)
        (current-age == tech-level-4)
        (population > unit-max)
        (carbon-amount >= 500)
        (nova-amount >= 500)
        (metal-amount >= 500)
        (cc-players-building-type-count every-ally BLDG-MONUMENT == 0)
        (cc-players-building-type-count every-enemy BLDG-MONUMENT == 0)
        (cc-players-building-type-count every-neutral BLDG-MONUMENT == 0)
        (not (goal personality 1) )
=>
        (set-goal upgrade-conflict NOTICE)
        (set-goal wonder-attempt POSSIBLE)
        (chat-to-player-using-id every-ally 22410)      ;200
        (disable-timer t-ageup)
        (disable-self)
)

#load-if-defined DEATHMATCH

(defrule
        (goal wonder-attempt MAYBE)
        (current-age == tech-level-4)
        (population > deathmatch-unit-max)
        (carbon-amount >= 500)
        (nova-amount >= 500)
        (metal-amount >= 500)
=>
        (set-goal upgrade-conflict NOTICE)
        (set-goal wonder-attempt POSSIBLE)
        (chat-to-player-using-id every-ally 22410)      ;200
        (disable-timer t-ageup)
        (disable-self)
)
#end-if

(defrule
        (goal wonder-attempt POSSIBLE)
=>
        (enable-timer t-ageup 1)
        (disable-self)
)

(defrule
        (timer-triggered t-ageup)
        (goal wonder-attempt POSSIBLE)
        (metal-amount < 1000)
=>
        (chat-to-player every-ally "6")
        (disable-timer t-ageup)
        (enable-timer t-ageup 150)
)

(defrule
        (timer-triggered t-ageup)
        (goal wonder-attempt POSSIBLE)
        (nova-amount < 1000)
=>
        (chat-to-player every-ally "5")
        (disable-timer t-ageup)
        (enable-timer t-ageup 150)
)

(defrule
        (timer-triggered t-ageup)
        (goal wonder-attempt POSSIBLE)
        (carbon-amount < 1000)
=>
        (chat-to-player every-ally "4")
        (disable-timer t-ageup)
        (enable-timer t-ageup 150)
)

(defrule
        (goal wonder-attempt POSSIBLE)
        (can-build BLDG-MONUMENT)
=>
        (set-strategic-number sn-maximum-town-size 10)
        (set-strategic-number sn-percent-civilian-builders 90)
        (set-strategic-number sn-cap-civilian-builders 50)
        (set-strategic-number sn-percent-civilian-gatherers 10)
        (set-goal wonder-attempt YES)
        (disable-timer t-ageup)
        (enable-timer t-ageup 30)
        (disable-self)
)

(defrule
        (goal wonder-attempt YES)
        (building-type-count BLDG-FARM > 0)
=>
        (delete-building BLDG-FARM)
)

(defrule
        (goal wonder-attempt YES)
        (building-type-count BLDG-FARM == 0)
        (can-build BLDG-MONUMENT)
=>
        (build BLDG-MONUMENT)
        (set-goal upgrade-conflict NO)
        (set-goal wonder-attempt NO)
        (set-goal resource-needed NO)
        (disable-self)
)

(defrule
        (building-type-count-total BLDG-MONUMENT > 0)
=>
        (set-strategic-number sn-maximum-town-size imperial-town-size)
        (disable-self)
)

(defrule
        (building-type-count BLDG-MONUMENT > 0)
=>
        (set-strategic-number sn-percent-civilian-builders 15)
        (set-strategic-number sn-cap-civilian-builders 8)
        (set-strategic-number sn-percent-civilian-gatherers 85)
        (disable-self)
)

;delete monument if someone else has beaten us to the punch
(defrule
        (building-type-count BLDG-MONUMENT == 0)
        (building-type-count-total BLDG-MONUMENT > 0)
        (cc-players-building-type-count any-ally BLDG-MONUMENT > 0)
        (or
                (enemy-captured-holocrons)
                (or
                        (cc-players-building-type-count any-enemy BLDG-MONUMENT > 0)
                        (cc-players-building-type-count any-neutral BLDG-MONUMENT > 0)
                )
        )
=>
        (delete-building BLDG-MONUMENT)
        (set-strategic-number sn-maximum-town-size imperial-town-size)
        (set-strategic-number sn-percent-civilian-builders 15)
        (set-strategic-number sn-cap-civilian-builders 8)
        (set-strategic-number sn-percent-civilian-gatherers 85)
        (set-goal wonder-attempt NO)
        (set-goal upgrade-conflict NO)
        (disable-self)
)

#end-if
#end-if
#end-if

(defrule
        (timer-triggered t-production)
        =>
        (disable-timer t-production)
        (enable-timer t-production 30)
)

